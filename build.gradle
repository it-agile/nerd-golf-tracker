task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
    def repositoryPath = 'http://localhost:8081/nexus/service/local/repositories/releases/content'
    distributionUrl = "${repositoryPath}/org/gradle/gradle/${gradleVersion}/gradle-${gradleVersion}-all.zip"
}


def apply (String... basenamesWithoutExt) {
    basenamesWithoutExt.each { apply from: "gradle/${it}.gradle" }
}
apply 'dslExtensions'


apply plugin: 'java'
defaultTasks 'clean', 'build'


sourceCompatibility = '1.6'
tasks.withType(JavaCompile) { 
    options.compilerArgs << "-Xlint:-options" 
    options.encoding = 'UTF-8'
}


repositories {
    maven { url "http://localhost:8081/nexus/content/groups/public/" }
    mavenCentral()
}
apply 'testDependencies'


archivesBaseName = "nerd-golf-tracker"
jar.manifest {
    attributes('Main-Class': 'de.itagile.golf.Main')
}
apply 'upload'


sourceSets.test.java { srcDir 'src/unittests/java' }
dependencies {
    testCompile junit, hamcrest
    testCompile 'org.mockito:mockito-core:1.8.5'
}

apply 'cucumber'


apply 'eclipse', 'idea'

