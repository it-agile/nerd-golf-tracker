apply plugin: 'java'
apply plugin: 'eclipse'

defaultTasks 'clean', 'build'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
    test {
        java {
            srcDir 'unittests'
            srcDir 'integration'
            srcDir 'specification'
        }
        resources {
            srcDir 'specification'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'org.jbehave', name: 'jbehave-core', version: '2.5.9'
    testCompile group: 'junit', name: 'junit', version: '4.8.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.8.5'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.5-RC2'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
test {
    systemProperty 'file.encoding', 'UTF-8'
}


jar {
    manifest {
        attributes('Main-Class': 'de.itagile.golf.Main')
        attributes('Class-Path': configurations.compile.collect{ it.name }.join(' '))
    }
}

task copyToLib(type: Copy) {
    from configurations.runtime
    into libsDir
}

jar.dependsOn copyToLib

tasks.eclipse.dependsOn(cleanEclipse)
eclipse.jdt {
    sourceCompatibility = 1.6
    targetCompatibility = 1.5
}
