sourceSets {
    cucumber {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}


class Cucumber extends JavaExec {
    def Cucumber() {
        project.tasks.check.dependsOn this
        mustRunAfter project.tasks.test

        setMain 'cucumber.api.cli.Main'
        classpath project.sourceSets.cucumber.runtimeClasspath
        dependsOn project.tasks.compileCucumberJava, project.tasks.jar

        args '--strict',
             '--glue', 'de.itagile.golf',
             '-f', "html:${project.reporting.baseDir}/${name}", 
             'src/cucumber/resources'

        logging.captureStandardOutput LogLevel.INFO
    }
}

dependencies {
    cucumberCompile 'info.cukes:cucumber-java:1.1.4'
    cucumberCompile 'info.cukes:cucumber-junit:1.1.4'
    cucumberCompile junit, hamcrest

    cucumberRuntime 'info.cukes:cucumber-picocontainer:1.1.4'
}

task cucumber(type: Cucumber) {
    args '--tags', '~@in_arbeit'
}
task cucumberInArbeit(type: Cucumber) {
    args '--tags', '@in_arbeit'
    ignoreExitValue = true
}

